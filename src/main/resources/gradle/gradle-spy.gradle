import groovy.json.JsonOutput

class BuildSpy {

    ServerSocket serverSocket
    Socket socket
    Writer socketWriter

    BuildSpy(int port) {
        try {
            serverSocket = new ServerSocket(port);
            println "Gradle build spy started on port $port"
            println "I'll be hanging around waiting for the g-viz to connect.."
        }
        catch (IOException ex) {
            throw new BuildCancelledException("Unable to listen to port " + port, ex)
        }

        socket = serverSocket.accept()
        socketWriter = socket.outputStream.newWriter("utf-8")
    }

    synchronized void send(String key, Object payload) {
        try {
            socketWriter.write(JsonOutput.toJson([
                    message: key,
                    payload: payload
            ]) + "\n")

            socketWriter.flush()

            // Thread.sleep(20); // REMOVE ME
        }
        catch (Exception ex) {
            throw new BuildCancelledException("failed to send message", ex)
        }
    }

}

BuildSpy spy = null




gradle.addListener(new BuildListener() {

    void buildStarted(Gradle gradle) {
        println "\nBuild started!\n"
    }

    void settingsEvaluated(Settings settings) {
        logger.lifecycle "Settings evaluated"

        String spyPort = System.getenv("SPY_PORT")

        if (!spyPort) {
            throw new BuildCancelledException("No environment variable SPY_PORT set")
        }

        spy = new BuildSpy(Integer.parseInt(spyPort))
        spy.send "project-name", settings.rootProject.name
        spy.send "max-worker-count", settings.startParameter.maxWorkerCount
    }

    void projectsLoaded(Gradle gradle) {
        println "Halla, I'm loaded!"


        gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
            def tasks = graph.allTasks.collect { Task task ->
                return [
                        name: task.name,
                        path: task.path,
                        description: task.description,
                        dependsOn: task.taskDependencies.getDependencies(task).collect { it.path }
                ]
            }

            spy.send "task-list", tasks


            graph.allTasks.forEach { Task task ->
                if (task instanceof Test) {
                    task.addTestListener(new TestListener() {
                        @Override
                        void beforeSuite(TestDescriptor suite) { }

                        @Override
                        void afterSuite(TestDescriptor suite, TestResult result) { }

                        @Override
                        void beforeTest(TestDescriptor testDescriptor) {
                            spy.send("before-test", [
                                    name: testDescriptor.name,
                                    className: testDescriptor.className
                            ])
                        }

                        @Override
                        void afterTest(TestDescriptor testDescriptor, TestResult result) {
                            spy.send("after-test", [
                                    name: testDescriptor.name,
                                    className: testDescriptor.className,
                                    result: result.resultType.name(),
                                    duration: result.endTime - result.startTime,
                                    exception: result.exception?.message
                            ])
                        }

                    })
                }
            }
        }




        gradle.taskGraph.addTaskExecutionListener(new TaskExecutionListener() {

            Map<String, Long> taskStartTime = [:]

            @Override
            void beforeExecute(Task task) {
                taskStartTime[task.path] = System.currentTimeMillis()

                spy.send("before-task", [
                        name: task.name,
                        path: task.path,
                        started: taskStartTime[task.path]
                ])
            }

            @Override
            void afterExecute(Task task, TaskState state) {
                spy.send("after-task", [
                        name: task.name,
                        path: task.path,
                        didWork: state.didWork,
                        executed: state.executed,
                        skipped: state.skipped,
                        skippedMessage: state.skipMessage,
                        failureMessage: state.failure?.message,
                        duration: System.currentTimeMillis() - taskStartTime[task.path]
                ])
            }

        })
    }

    void projectsEvaluated(Gradle gradle) {
        println "Projects evaluated!"
    }

    void buildFinished(BuildResult result) {
        println "Finished"
    }

})


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

project.ext {
    dropwizardVersion = '0.8.2'
    jettyVersion = '9.2.9.v20150224' // sync with dropwizard
}

mainClassName = 'com.developerb.gviz.GVizApplication'

repositories {
    jcenter()
}

dependencies {

    // dropwizard
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-views-freemarker:' + dropwizardVersion,
            'io.dropwizard:dropwizard-assets:' + dropwizardVersion

    // jetty
    compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:' + jettyVersion

    // utils
    compile 'org.zeroturnaround:zt-exec:1.8'

    // test
    testCompile 'junit:junit:4.12'
    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

// Configure the shadow jar task
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

run {
    args 'server'
}
